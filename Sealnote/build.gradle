buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
    }
}
apply plugin: 'android'

repositories {
    jcenter()
}

android {
    compileSdkVersion 19
    buildToolsVersion '21.0.0'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19

        versionCode 26
        versionName "0.8.6"
    }

    signingConfigs {
        release {
            storeFile file(String.valueOf(System.getenv("SEALNOTE_KEYSTORE")))
            storePassword System.getenv("SEALNOTE_KEYSTORE_PASSWORD")
            keyAlias System.getenv("SEALNOTE_KEY_ALIAS")
            keyPassword System.getenv("SEALNOTE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
        }
    }


    lintOptions {
        //checkReleaseBuilds false

        // This is temporary
        abortOnError false
    }
}

task nativeLibsToJar(type: Zip, description: 'create a jar archive of the native libs') {
    destinationDir file("$buildDir/native-libs")
    baseName 'native-libs'
    extension 'jar'
    from fileTree(dir: 'libs', include: '**/*.so')
    into 'lib/'
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn(nativeLibsToJar)
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile fileTree(dir: "$buildDir/native-libs", include: 'native-libs.jar')

    compile 'com.android.support:support-v4:20.0.+'
    compile 'com.etsy.android.grid:library:1.0.4'
    compile 'com.github.gabrielemariotti.cards:library:1.6.0'
    compile 'com.github.gabrielemariotti.cards:library-extra:1.6.0'
    compile 'com.nhaarman.listviewanimations:library:2.6.0'

    compile 'com.github.codechimp-org.apprater:library:1.0.+'

    compile project(':aFileChooser')
}
